set(LINKER_CANDICATES "gold" "lld" "mold" "bfd")
list(JOIN LINKER_CANDICATES " " linker_candicates_str_list)
set(NEBULA_USE_LINKER
    "gold"
    CACHE STRING "Choose the linker, options are: ${linker_candicates_str_list}")

list(FIND LINKER_CANDICATES ${NEBULA_USE_LINKER} index)
if(index EQUAL -1)
    message(FATAL_ERROR "Unknown linker: ${NEBULA_USE_LINKER}")
endif()

print_config(NEBULA_USE_LINKER)

if (${NEBULA_USE_LINKER} STREQUAL "mold")
  include(FindMoldLinker)
else()
  nebula_add_exe_linker_flag(-fuse-ld=${NEBULA_USE_LINKER})
  nebula_add_shared_linker_flag(-fuse-ld=${NEBULA_USE_LINKER})
endif()

# nebula_add_exe_linker_flag(-no-pie)
nebula_add_exe_linker_flag(-rdynamic)
nebula_add_exe_linker_flag(-Wl,--as-needed)
nebula_add_exe_linker_flag(-Wl,--build-id)
nebula_add_shared_linker_flag(-Wl,--build-id)
nebula_add_shared_linker_flag(-Wl,-Bsymbolic-functions)

if(NOT ${CMAKE_BUILD_TYPE} STREQUAL "Debug")
    add_definitions(-D_FORTIFY_SOURCE=2)
else()
    # The mips need to add it when build Debug to lift the usual restrictions on the size of the global offset table.
    if (${CMAKE_HOST_SYSTEM_PROCESSOR} MATCHES "mips64")
        add_compile_options(-mxgot)
    endif()
endif()

if(CMAKE_BUILD_TYPE STREQUAL "Debug" OR CMAKE_BUILD_TYPE STREQUAL "RelWithDebInfo")
    if (NOT ${NEBULA_USE_LINKER} STREQUAL "bfd")
        nebula_add_exe_linker_flag(-Wl,--gdb-index)
        nebula_add_shared_linker_flag(-Wl,--gdb-index)
    else()
        nebula_remove_exe_linker_flag(-Wl,--gdb-index)
        nebula_remove_shared_linker_flag(-Wl,--gdb-index)
    endif()
    if (NOT ${CMAKE_HOST_SYSTEM_PROCESSOR} MATCHES "mips64")
        if(ENABLE_COMPRESSED_DEBUG_INFO)
            add_compile_options(-Wa,--compress-debug-sections=zlib)
            nebula_add_exe_linker_flag(-Wl,--compress-debug-sections=zlib)
            nebula_add_shared_linker_flag(-Wl,--compress-debug-sections=zlib)
        else()
            nebula_remove_exe_linker_flag(-Wl,--compress-debug-sections=zlib)
            nebula_remove_shared_linker_flag(-Wl,--compress-debug-sections=zlib)
        endif()
    endif()
    if (CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
        add_compile_options(-fno-limit-debug-info)
    endif()
endif()
